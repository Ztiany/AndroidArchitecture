apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.getName())
    }
}

boolean openDebug = isOpenDebug.toBoolean()
boolean showDebugTools = showDebugTools.toBoolean()
boolean trustHttpsCertification = httpsTrustAll.toBoolean()
boolean openLog = isOpenLog.toBoolean()
String specifiedHost = HOST_ENV

android {

    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        buildConfigField("boolean", "openDebug", "$openDebug")
        buildConfigField("boolean", "showDebugTools", "$showDebugTools")
        buildConfigField("boolean", "trustHttpsCertification", "$trustHttpsCertification")
        buildConfigField("boolean", "openLog", "$openLog")
        buildConfigField("String", "specifiedHost", "\"$specifiedHost\"")
        vectorDrawables.useSupportLibrary = true
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main {
            java.srcDirs += "src/github/java"
            res.srcDirs += "src/github/res"

            java.srcDirs += "src/common/java"
            res.srcDirs += "src/common/res"

            if (openDebug) {
                java.srcDirs += "src/tools-debug/java"
                res.srcDirs += "src/tools-debug/res"
            } else {
                java.srcDirs += "src/tools-release/java"
                res.srcDirs += "src/tools-release/res"
            }
        }
    }

    //如果不想生成某个布局的绑定类，可以在根视图添加 tools:viewBindingIgnore="true" 属性。
    viewBinding {
        enabled = true
    }

}

dependencies {
    //测试
    testImplementation testLibraries.junit

    //基础组件库
    api project(':lib_base')//基础类库
    api project(':lib_common_ui')//基础UI
    api project(':lib_network')//基础网络库
    api project(':lib_permission')//权限
    api project(':lib_cache')//缓存库
    api project(':lib_media_selector')//媒体选择器
    api project(':lib_social')//第三方登录
    api project(':lib_upgrade')//升级
    api project(':lib_safekeyboard')//安全键盘
    api project(':lib_biometrics')//生物识别
    api project(':lib_webview')//web view
    api project(':lib_qrcode')//二维码

    //Android
    api androidLibraries.multidex
    api androidLibraries.exifinterface
    api androidLibraries.flexbox
    api androidLibraries.ktx
    api androidLibraries.room
    api androidLibraries.roomKtx

    /*Hilt*/
    api androidLibraries.hiltDagger
    kapt androidLibraries.hiltDaggerApt
    api androidLibraries.hiltViewiewModel
    kapt androidLibraries.hiltApt

    /*ARouter*/
    api thirdLibraries.arouter
    kapt thirdLibraries.arouterAnnotation

    /*OkHttp*/
    implementation thirdLibraries.okHttp
    implementation thirdLibraries.okHttpLogging

    /*Gson*/
    api thirdLibraries.gson

    /*Retrofit*/
    api thirdLibraries.retrofit
    implementation thirdLibraries.retrofitConverterGson
    implementation thirdLibraries.retrofitRxJava2CallAdapter
    /*RxKotlin, RxBinding*/
    api thirdLibraries.rxBindingCompat
    api kotlinLibraries.rxKotlin

    /*GlideAPT*/
    kapt thirdLibraries.glideApt

    /*UI*/
    api uiLibraries.qmui
    api uiLibraries.circleImageView
    api uiLibraries.keyboardVisibilityEvent

    //image compression
    implementation 'id.zelory:compressor:3.0.0'

    //----------------third-------------
    //channel
    implementation 'com.leon.channel:helper:2.0.3'
    //bugly
    implementation 'com.tencent.bugly:crashreport:3.2.422'
    //Umeng push
    implementation 'com.umeng.umsdk:common:9.3.0'
    implementation 'com.umeng.umsdk:utdid:1.5.2'
    implementation 'com.umeng.umsdk:asms:1.1.3'
    implementation 'com.umeng.umsdk:crash:0.0.5'
    implementation 'com.umeng.umsdk:push:6.2.0'
    implementation 'com.umeng.umsdk:alicloud-httpdns:1.2.5'
    implementation 'com.umeng.umsdk:alicloud-utils:1.1.5'
    implementation 'com.umeng.umsdk:alicloud_beacon:1.0.1'
    implementation 'com.umeng.umsdk:agoo-accs:3.3.9.7-open'
    implementation 'com.umeng.umsdk:agoo_networksdk:3.5.5'
    implementation 'com.umeng.umsdk:agoo_tlog:3.0.0.17'
    implementation 'com.umeng.umsdk:agoo_tnet4android:3.1.14.9'

    //Debug
    if (openDebug) {
        //Stetho
        implementation debugLibraries.stetho
        implementation debugLibraries.stethoOkhttp3
        //LeakCanary
        implementation debugLibraries.debugLeakCanary
        //UETool
        implementation debugLibraries.ueTool
    }

}